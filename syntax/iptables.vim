"============================================================================
"
" iptables-save/restore syntax highlighter
"
" Language:	   iptables-save/restore file
" Version:     Not Specified
" Date:        07-Jun-2014
" Maintainer:  Eric Haarbauer <ehaar70{AT}gmail{DOT}com>
" License:     This file is placed in the public domain.
"
"============================================================================
" Section:  Notes  {{{1
"============================================================================
"
" This vim syntax script highlights files used by Harald Welte's iptables-save
" and iptables-restore utilities.  Both utilities are part of the iptables
" application (http://www.netfilter.org/projects/iptables).
" 
" Features:
"
"   * Distinguishes commands, options, modules, targets and chains.
"   * Distinguishes numeric IP addresses from net masks.
"   * Highlights tokens that occur only in hand-edited files; for example,
"     "--append" and "destination-unreachable".
"   * Special handling for module names; for example, the tcp module is
"     colored differently from the tcp protocol.
"
" Options:
"
"   Customize the behavior of this script by setting values for the following
"   options in your .vimrc file.  (Type ":h vimrc" in vim for more information
"   on the .vimrc file.)
"
"   g:Iptables_SpecialDelimiters
"     This variable, if set to a non-zero value, distinguishes numeric
"     delimiters, including the dots in IP addresses, the slash that separates
"     an IP address from a netmask, and the colon that separates the ends of a
"     port range.  If not set, this option defaults to off.
"
" Known Issues:
"
"   * Some special argument tokens are highlighted whether or not they are
"     used with the correct option.  For example, "destination-unreachable"
"     gets special highlighting whether or not is used as an argument to the
"     --icmp-type option.  In practice, this is rarely a problem.
"
" Reporting Issues:
"
"   If you discover an iptables file that this script highlights incorrectly,
"   please email the author (address at the top of the script) with the
"   following information:
"
"     * Problem iptables file WITH ANY SENSITIVE INFORMATION REMOVED
"     * The release version of this script (see top of the script)
"     * If possible, a patch to fix the problem
"
" Design Notes:
"
"   Part of this script is autogenerated from the output of the iptables man
"   page.  The source code for generating the script is available from the
"   author on request (see email address at the top of the script).  The
"   script should build from source on most Linux systems with iptables
"   installed.
"
"   The build system that generates this script strips special CVS tokens
"   (like "Id:") so that CVS no longer recognizes them.  This allows users to
"   place the script in their own version control system without losing
"   information.  The author encourages other vim script developers to adopt a
"   similar approach in their own scripts.
"
" Installation:
"
"   Put this file in your user runtime syntax directory, usually ~/.vim/syntax
"   in *NIX or C:\Program Files\vim\vimfiles\syntax in Windows.  Type ":h
"   syn-files" from within vim for more information.
"
"   The iptables-save and iptables-restore applications do not specify a
"   naming standard for the files they use.  However, iptables-save places a
"   comment in the first line of its output.  Other applications, such as
"   Fedora's system-config-securitylevel uses the iptables-save/restore
"   format, but with a different leading comment.  We can use these leading
"   comments to identify the filetype by placing the following code in the
"   scripts.vim file in your user runtime directory:
"   
"      if getline(1) =~ "^# Generated by iptables-save" ||
"       \ getline(1) =~ "^# Firewall configuration written by"
"          setfiletype iptables
"          set commentstring=#%s
"          finish
"      endif
"
"   Setting the commentstring on line 4 allows Meikel Brandmeyer's
"   EnhancedCommentify script (vimscript #23) to work with iptables files.
"   (Advanced users may want to set the commentstring option in an ftplugin
"   file or in autocommands defined in .vimrc.)
"
"============================================================================
" Source File: Id: iptables.src.vim 43 2014-06-08 03:21:32Z ehaar 
"============================================================================
" Section:  Initialization  {{{1
"============================================================================

" For version 5.x: Clear all syntax items
" For version 6.x: Quit when a syntax file was already loaded
if !exists("main_syntax")
  if version < 600
    syntax clear
  elseif exists("b:current_syntax")
    finish
  endif
  let main_syntax = 'iptables'
endif

" Don't use standard HiLink, it will not work with included syntax files
if version < 508
  command! -nargs=+ IptablesHiLink highlight link <args>
else
  command! -nargs=+ IptablesHiLink highlight default link <args>
endif

syntax case match

if version < 600
    set iskeyword+=-
else
    setlocal iskeyword+=-
endif

" Initialize global public variables:  {{{2

" Support deprecated variable name used prior to release 1.07.
if exists("g:iptablesSpecialDelimiters") &&
\ !exists("g:Iptables_SpecialDelimiters")

    let   g:Iptables_SpecialDelimiters = g:iptablesSpecialDelimiters
    unlet g:iptablesSpecialDelimiters
    " echohl WarningMsg | echo "Warning:" | echohl None
    " echo "The g:iptablesSpecialDelimiters variable is deprecated."
    " echo "Please use g:Iptables_SpecialDelimiters in your .vimrc instead"

endif

if exists("g:Iptables_SpecialDelimiters")
    let s:Iptables_SpecialDelimiters = g:Iptables_SpecialDelimiters
else
    let s:Iptables_SpecialDelimiters = 0
endif

"============================================================================
" Section:  Group Definitions  {{{1
"============================================================================

syntax keyword iptablesSaveDirective COMMIT
syntax match   iptablesSaveOperation "^[:*]"

syntax keyword iptablesTable filter nat mangle raw

syntax keyword iptablesTarget
    \ ACCEPT DROP QUEUE RETURN BALANCE CLASSIFY CLUSTERIP CONNMARK
    \ CONNSECMARK CONNTRACK DNAT DSCP ECN IPMARK IPV4OPSSTRIP LOG
    \ MARK MASQUERADE MIRROR NETMAP NFQUEUE NOTRACK REDIRECT REJECT
    \ ROUTE SAME SECMARK SET SNAT TARPIT TCPMSS TOS TRACE TTL ULOG XOR
    \ NFLOG AUDIT CHECKSUM CT DNPT HL HMARK IDLETIMER LED RATEEST
    \ SNPT TCPOPTSTRIP TEE TPROXY

syntax keyword iptablesBuiltinChain
    \ INPUT OUTPUT FORWARD PREROUTING POSTROUTING

syntax keyword iptablesCommand -A -D -I -R -L -F -Z -N -X -P -E
    \ --append --delete --insert --replace --list --flush --zero
    \ --new-chain --delete-chain --policy --rename-chain

syntax keyword iptablesParam   -p -s -d -j -i -o -f -c -t

syntax match iptablesOperator "\s\zs!\ze\s"

syntax keyword iptablesModuleName contained
    \ account addrtype ah childlevel comment condition connbytes connlimit
    \ connmark connrate conntrack dccp dscp dstlimit ecn esp fuzzy hashlimit
    \ helper icmp iprange ipv4options length limit mac mark mport multiport
    \ nth osf owner physdev pkttype policy psd quota random realm recent
    \ sctp set state string tcp tcpmss time tos ttl u32 udp unclean bpf
    \ cluster connlabel cpu devgroup dst eui64 frag hbh hl ipv6header
    \ ipvs mh nfacct rateest rpfilter rt socket statistic

syntax keyword iptablesModuleType
    \ UNSPEC UNICAST LOCAL BROADCAST ANYCAST MULTICAST BLACKHOLE UNREACHABLE
    \ PROHIBIT THROW NAT XRESOLVE INVALID ESTABLISHED NEW RELATED SYN ACK FIN
    \ RST URG PSH ALL NONE

" From --reject-with option
syntax keyword iptablesModuleType
    \ icmp-net-unreachable
    \ icmp-host-unreachable
    \ icmp-port-unreachable
    \ icmp-proto-unreachable
    \ icmp-net-prohibited
    \ icmp-host-prohibited
    \ icmp-admin-prohibited

" From --icmp-type option
syntax keyword iptablesModuleType
    \ any
    \ echo-reply
    \ destination-unreachable
    \    network-unreachable
    \    host-unreachable
    \    protocol-unreachable
    \    port-unreachable
    \    fragmentation-needed
    \    source-route-failed
    \    network-unknown
    \    host-unknown
    \    network-prohibited
    \    host-prohibited
    \    TOS-network-unreachable
    \    TOS-host-unreachable
    \    communication-prohibited
    \    host-precedence-violation
    \    precedence-cutoff
    \ source-quench
    \ redirect
    \    network-redirect
    \    host-redirect
    \    TOS-network-redirect
    \    TOS-host-redirect
    \ echo-request
    \ router-advertisement
    \ router-solicitation
    \ time-exceeded
    \    ttl-zero-during-transit
    \    ttl-zero-during-reassembly
    \ parameter-problem
    \    ip-header-bad
    \    required-option-missing
    \ timestamp-request
    \ timestamp-reply
    \ address-mask-request
    \ address-mask-reply

" If we used a keyword for this, port names would be colored the same
" as modules with the same name (e.g. tcp, udp, icmp).
syntax keyword iptablesParam -m --match skipwhite nextgroup=iptablesModuleName

syntax region iptablesString start=+"+ skip=+\\"+ end=+"+ oneline

syntax match  iptablesComment    "^#.*" contains=iptablesTodo
syntax match  iptablesBadComment "^\s\+\zs#.*" " Pound must be in first column

syntax keyword iptablesTodo contained TODO FIXME XXX NOT NOTE

" Special Delimiters: {{{2

if s:Iptables_SpecialDelimiters != 0
    syntax match iptablesNumber    "\<[0-9./:]\+\>"
        \ contains=iptablesMask,iptablesDelimiter
    syntax match iptablesDelimiter "[./:]"     contained
    syntax match iptablesMask      "/[0-9.]\+" contained 
        \ contains=iptablesDelimiter
else " s:Iptables_SpecialDelimiters == 0
    syntax match iptablesNumber    "\<[0-9./]\+\>"
        \ contains=iptablesMask,iptablesDelimiter
    syntax match iptablesDelimiter "/"         contained
    syntax match iptablesMask      "/[0-9.]\+" contained 
        \ contains=iptablesDelimiter
endif

"============================================================================
" Section:  Autogenerated Groups  {{{2
"============================================================================

" Begin autogenerated section.
" updated:      "01JAN2016"
" iptables:     "iptables v1.4.21"

syntax keyword iptablesLongParam
   \ --match --help --protocol --src-type --dst-type --limit-iface-in
   \ --limit-iface-out --limit-iface-out --limit-iface-in --ahspi --ahlen --ahres
   \ --ahspi --bytecode --bytecode --bytecode --cluster-total-nodes
   \ --cluster-local-node --cluster-local-nodemask --cluster-local-node.
   \ --cluster-hash-seed --cluster-total-nodes --cluster-local-node
   \ --cluster-hash-seed --set-mark --cluster-total-nodes --cluster-local-node
   \ --cluster-hash-seed --set-mark --mark --mark --h-length --mangle-mac-s
   \ --h-length --destination-mac --mangle-mac-d --h-length --mangle-mac-s
   \ --h-length --destination-mac --mangle-mac-d --comment --comment --connbytes
   \ --connbytes-dir --connbytes-mode --connbytes --connbytes-dir --connbytes-mode
   \ --label --set --label --connlimit-upto --connlimit-above --connlimit-mask
   \ --connlimit-saddr --connlimit-daddr --connlimit-daddr --syn --dport
   \ --connlimit-above --syn --dport --connlimit-upto --syn --dport
   \ --connlimit-above --connlimit-mask --syn --dport --connlimit-above
   \ --connlimit-mask --syn --dport --connlimit-above --mark --ctstate --ctproto
   \ --ctorigsrc --ctorigdst --ctreplsrc --ctrepldst --ctorigsrcport --ctorigdstport
   \ --ctreplsrcport --ctrepldstport --ctstatus --ctexpire --ctdir --ctstate:
   \ --notrack --ctstatus: --cpu --dport --cpu --to-port --dport --cpu --to-port
   \ --source-port -sport --destination-port -dport --dccp-types --dccp-option
   \ --src-group --dst-group --dscp --dscp-class --dst-len --dst-opts --ecn-tcp-cwr
   \ --ecn-tcp-ece --ecn-ip-ect --espspi --fragid --fraglen --fragres --fragfirst
   \ --fragmore --fraglast --hashlimit-upto --hashlimit-above --hashlimit-name
   \ --hashlimit-upto --hashlimit-above --hashlimit-burst -- --hashlimit-mode
   \ --hashlimit-mode --hashlimit-srcmask --hashlimit-mode --hashlimit-srcmask
   \ --hashlimit-mode --hashlimit-dstmask --hashlimit-srcmask --hashlimit-name
   \ --hashlimit-htable-size --hashlimit-htable-max --hashlimit-htable-expire
   \ --hashlimit-htable-gcinterval --hashlimit-mode --hashlimit-upto
   \ --hashlimit-mode --hashlimit-upto --hashlimit-mask --hashlimit-upto
   \ --hashlimit-mode --hashlimit-above --hashlimit-mode --hashlimit-above
   \ --hashlimit-burst --hbh-len --hbh-opts --helper --hl-eq --hl-lt --hl-gt
   \ --protocol --icmp-type --protocol --protocol --icmpv6-type --src-range
   \ --dst-range --soft --header. --header --ipvs --ipvs --vproto --vaddr --vport
   \ --vdir --vmethod --vportctl --length --hashlimit --hashlimit-mode. --limit
   \ --limit-burst --mac-source --mark --protocol --protocol --mh-type
   \ --source-ports -sports --sports --destination-ports -dports --dports --ports
   \ --nfacct-name --sport --nfacct-name --dport --nfacct-name --genre --ttl --log
   \ --uid-owner --uid-owner --gid-owner --gid-owner --socket-exists --physdev-in
   \ --physdev-out --physdev-is-in --physdev-is-out --physdev-is-bridged --pkt-type
   \ --dir --pol --pol --strict. --strict --strict --reqid --spi --proto --mode
   \ --tunnel-src --mode --tunnel-dst --mode --next --strict. --quota
   \ --rateest-delta --rateest-lt --rateest-gt --rateest-eq --rateest --rateest1
   \ --rateest2 --rateest-bps --rateest-pps --rateest-bps1 --rateest-bps2
   \ --rateest-pps1 --rateest-pps2 --rateest-name --rateest-interval --rateest-ewma
   \ --rateest-name --rateest-interval --rateest-ewma --ctstate --helper
   \ --rateest-delta --rateest1 --rateest-bps1 --rateest-gt --rateest2
   \ --rateest-bps2 --set-mark --ctstate --helper --rateest-delta --rateest1
   \ --rateest-bps1 --rateest-gt --rateest2 --rateest-bps2 --set-mark --restore-mark
   \ --realm --set --rcheck --update --remove --name --set --rsource --rdest
   \ --mask --rcheck --update --rcheck --remove --seconds --rcheck --update. --reap
   \ --seconds. --hitcount --rcheck --update. --seconds --rttl --rcheck --update.
   \ --set --name --rcheck --seconds --dport --name --set --loose --validmark
   \ --accept-local --invert --limit --nflog-prefix --invert --rt-type --rt-segsleft
   \ --rt-len --rt-0-res --rt-0-addrs --rt-0-not-strict --source-port -sport
   \ --destination-port -dport --chunk-types --dport --chunk-types --chunk-types
   \ --match-set --match-set --return-nomatch --return-nomatch --update-counters
   \ --update-counters --update-subcounters --update-subcounters --packets-eq
   \ --packets-lt --packets-gt --bytes-lt --bytes-gt --match-set --set --transparent
   \ --nowildcard --transparent --transparent --set-mark --state --mode --mode
   \ --probability --every --packet --packet --algo --from --to --string
   \ --hex-string --dport --algo --string --dport --algo --from --to --hex-string
   \ --protocol --source-port -sport --sport --destination-port -dport --dport
   \ --tcp-flags --tcp-flags --syn --tcp-flags --syn" --tcp-option --mss
   \ --datestart --datestop --datestart --datestop --timestart --timestop
   \ --monthdays --weekdays --contiguous --timestop --timestart --kerneltz
   \ --kerneltz --weekdays --datestart --datestop --datestart --datestop --timestart
   \ --timestop --weekdays --monthdays --weekdays --timestart --timestop
   \ --contiguous --tos --tos --ttl-eq --ttl-gt --ttl-lt --u32 --u32 --u32 --u32
   \ --protocol --source-port -sport --source-port --destination-port -dport
   \ --destination-port --type --type --checksum-fill --set-class --new --hashmode
   \ --clustermac --total-nodes --local-node --hash-init --set-xmark --save-mark
   \ --nfmask --ctmask --restore-mark --nfmask --ctmask --restore-mark --set-xmark:
   \ --and-mark --set-xmark --or-mark --set-xmark --xor-mark --set-xmark --set-mark
   \ --save-mark --mask --restore-mark --mask --save --restore --notrack --helper
   \ --ctevents --expevents --zone --timeout --to-destination --to-destination
   \ --to-destination --random --random --persistent --src-pfx --dst-pfx --src-pfx
   \ --dst-pfx --src-pfx --dst-pfx --set-dscp --set-dscp-class --ecn-tcp-remove
   \ --hl-set --hl-dec --hl-inc --hmark-tuple --hmark-mod --hmark-offset
   \ --hmark-tuple --hmark-src-prefix --hmark-dst-prefix --hmark-sport-mask
   \ --hmark-dport-mask --hmark-spi-mask --hmark-proto-mask --hmark-rnd --ctstate
   \ --hmark-tuple --hmark-offset --hmark-mod --hmark-rnd --hmark-offset
   \ --hmark-tuple --hmark-mod --hmark-rnd --timeout --label --led-trigger-id
   \ --led-delay --led-always-blink --dport --led-trigger-id --log-level
   \ --log-prefix --log-tcp-sequence --log-tcp-options --log-ip-options --log-uid
   \ --set-xmark --set-mark --and-mark --set-xmark --or-mark --set-xmark --xor-mark
   \ --set-xmark --to-ports --random --random --to --nflog-group --nflog-prefix
   \ --nflog-range --nflog-threshold --queue-num --queue-balance --queue-balance
   \ --queue-bypass --queue-cpu-fanout --queue-balance --queue-balance --notrack.
   \ --rateest-name --rateest-interval --rateest-ewmalog --to-ports --random
   \ --random --reject-with --reject-with --to --persistent --to --nodst --random
   \ --selctx --add-set --del-set --timeout --exist --to-source --to-source
   \ --to-source --random --random --persistent --src-pfx --dst-pfx --src-pfx
   \ --dst-pfx --src-pfx --dst-pfx --tcp-flags --clamp-mss-to-pmtu --set-mss
   \ --clamp-mss-to-pmtu --strip-options --gateway --gateway --set-tos --set-tos
   \ --and-tos --set-tos --or-tos --set-tos --xor-tos --set-tos --set-tos --on-port
   \ --on-ip --tproxy-mark --ttl-set --ttl-dec --ttl-inc --ulog-nlgroup
   \ --ulog-prefix --ulog-cprange --ulog-qthreshold
   \ --table --append --check --delete --delete --insert --replace --list
   \ --list-rules --flush --zero --list --new-chain --delete-chain --policy
   \ --rename-chain --ipv4 --ipv6 --protocol --source --src --destination --dst
   \ --match --jump --goto --jump --jump. --in-interface --out-interface --fragment
   \ --set-counters --verbose --wait --numeric --exact --line-numbers
   \ --modprobe
   \ --addr --tname --addr --tname --addr --tname --delude --tarpit --set-mac
   \ --dport --physdev-in --mac --set-mac --dport --physdev-out --mac --set-mac
   \ --prefix --reuse --persistent --static --ttl --prefix --reuse --ttl --prefix
   \ --reuse --ttl --prefix --reuse --prefix --prefix --prefix --static --prefix
   \ --persistent --prefix --r-- --r-- --addr --and-mask --or-mask --shift
   \ --set-mark --set-mark --addr --and-mask --or-mask --addr --shift --and-mask
   \ --log-level --log-prefix --mac-source --dport --dir --pol --proto --tunnel-src
   \ --tunnel-dst --dport --mac-source --dport --limit --tarpit --honeypot --reset
   \ --dport --dport --notrack --dport --dport --condition --mac --lower-limit
   \ --upper-limit --src-cc --source-country --dst-cc --destination-country
   \ --enabled --disabled --iface --dev-in --dev-out --up --down --broadcast
   \ --loopback --pointtopoint --running --noarp --arp --promisc --multicast
   \ --dynamic --lower-up --dormant --edk --kazaa --gnu --dc --bit --apple --soul
   \ --winmx --ares --debug --ipp2p --flags --any --any --flags --flags --any
   \ --flags --flags --any --length --layer3 --layer4 --layer5 --layer* --layer3
   \ --layer5 --stealth --synscan --cnscan --grscan --psd-weight-threshold
   \ --psd-delay-threshold --psd-lo-ports-weight --psd-hi-ports-weight --grow
   \ --no-change --name --quota --grow --packets --dport --name --grow; --sport
   \ --name --knockports --strict --name --time --autoclose --dport --knockports
   \ --name --opensecret --closesecret --autoclose --checkip --name --dport
   \ --autoclose --time --time --strict
" End autogenerated section.

"============================================================================
" Section:  Group Linking  {{{1
"============================================================================

IptablesHiLink iptablesSaveDirective PreProc
IptablesHiLink iptablesSaveOperation PreProc

IptablesHiLink iptablesTable         Statement
IptablesHiLink iptablesTarget        Statement
IptablesHiLink iptablesBuiltinChain  Type

IptablesHiLink iptablesCommand       Operator

IptablesHiLink iptablesModuleName    Type
IptablesHiLink iptablesModuleType    Type

IptablesHiLink iptablesOperator      Operator
IptablesHiLink iptablesParam         Identifier
IptablesHiLink iptablesLongParam     Identifier

IptablesHiLink iptablesNumber        Constant

if s:Iptables_SpecialDelimiters != 0
    IptablesHiLink iptablesMask      PreProc
    IptablesHiLink iptablesDelimiter Delimiter
else " s:Iptables_SpecialDelimiters == 0 
    IptablesHiLink iptablesMask      Special
    IptablesHiLink iptablesDelimiter None
endif

IptablesHiLink iptablesString        Constant

IptablesHiLink iptablesComment       Comment
IptablesHiLink iptablesBadComment    Error
IptablesHiLink iptablesTodo          Todo   

"============================================================================
" Section:  Clean Up    {{{1
"============================================================================

delcommand IptablesHiLink

let b:current_syntax = "iptables"

if main_syntax == 'iptables'
  unlet main_syntax
endif

" Autoconfigure vim indentation settings
" vim:ts=4:sw=4:sts=4:fdm=marker:iskeyword+=-
